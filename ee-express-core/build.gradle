repositories {
    mavenCentral()
}

dependencies {
    api 'jakarta.inject:jakarta.inject-api:2.0.1'
    api 'jakarta.json.bind:jakarta.json.bind-api:3.0.1'
    api 'jakarta.json:jakarta.json-api:2.1.3'
    api 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    api 'org.slf4j:slf4j-api:2.0.17'

    runtimeOnly 'org.apache.johnzon:johnzon-jsonb:2.0.2'
    runtimeOnly 'org.apache.johnzon:johnzon:2.0.2'
    runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.5'
    runtimeOnly 'ch.qos.logback:logback-classic:1.5.18'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.19.0'
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(Test).configureEach {
    def mockitoAgent = configurations.testRuntimeClasspath.find {
        it.name.contains('mockito-core')
    }
    if (mockitoAgent) {
        jvmArgs "-javaagent:${mockitoAgent.absolutePath}"
    }

    // Needed in de.simonkerstan.ee.core.EeExpressApplicationIntegrationTest
    environment "MY_ENV_VARIABLE", "Hello World!"
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    source = sourceSets.main.allJava
    options {
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        author = true
        version = true
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'Simon Kerstan EE Express: Core module'
                description = 'A lightweight and minimal Jakarta EE runtime for Java'
                url = 'https://github.com/Simix2017/simon-kerstan-ee-express'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit'
                    }
                }
                developers {
                    developer {
                        id = 'Simix2017'
                        name = 'Simon Kerstan'
                        email = 's.kerstan2018@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Simix2017/simon-kerstan-ee-express.git'
                    developerConnection = 'scm:git:ssh://github.com:Simix2017/simon-kerstan-ee-express.git'
                    url = 'https://github.com/Simix2017/simon-kerstan-ee-express'
                }
            }
        }
    }
    repositories {
        maven {
            name = "LocalMavenWithChecksums"
            url = uri(layout.buildDirectory.dir("staging-deploy"))
        }
    }
}

signing {
    sign publishing.publications.maven
}
