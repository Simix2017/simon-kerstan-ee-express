repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.hibernate.orm:hibernate-processor:7.1.0.Final'

    api 'jakarta.persistence:jakarta.persistence-api:3.2.0'
    api 'jakarta.data:jakarta.data-api:1.0.1'

    implementation project(':ee-express-core')
    implementation project(':ee-express-validation')
    implementation 'org.hibernate.orm:hibernate-core:7.1.0.Final'
    implementation 'org.hibernate.orm:hibernate-c3p0:7.1.0.Final'

    testAnnotationProcessor 'org.hibernate.orm:hibernate-processor:7.1.0.Final'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.19.0'

    testRuntimeOnly 'com.h2database:h2:2.3.232'
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(Test).configureEach {
    def mockitoAgent = configurations.testRuntimeClasspath.find {
        it.name.contains('mockito-core')
    }
    if (mockitoAgent) {
        jvmArgs "-javaagent:${mockitoAgent.absolutePath}"
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    source = sourceSets.main.allJava
    options {
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        author = true
        version = true
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'Simon Kerstan EE Express: Persistence module'
                description = 'A lightweight and minimal Jakarta EE runtime for Java'
                url = 'https://github.com/Simix2017/simon-kerstan-ee-express'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit'
                    }
                }
                developers {
                    developer {
                        id = 'Simix2017'
                        name = 'Simon Kerstan'
                        email = 's.kerstan2018@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Simix2017/simon-kerstan-ee-express.git'
                    developerConnection = 'scm:git:ssh://github.com:Simix2017/simon-kerstan-ee-express.git'
                    url = 'https://github.com/Simix2017/simon-kerstan-ee-express'
                }
            }
        }
    }
    repositories {
        maven {
            name = "LocalMavenWithChecksums"
            url = uri(layout.buildDirectory.dir("staging-deploy"))
        }
    }
}

signing {
    sign publishing.publications.maven
}
